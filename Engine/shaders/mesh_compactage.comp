#version 450

#extension GL_EXT_control_flow_attributes:enable

layout(local_size_x = 64) in;

struct IndirectCommand
{
  uint indexCount;
};

struct Descriptor
{
  uint drawId;
  uint ptr;
  uint indSz;
};

struct VkDrawIndexedIndirectCommand
{
  uint indexCount;
  uint instanceCount;
  uint firstIndex;    // prefix sum
  int  vertexOffset;  // can be abused to offset into var_buffer
  uint firstInstance; // caps: should be zero
  uint vboOffset;     // prefix sum
};

layout(binding = 0, std430) restrict writeonly buffer EngineInternal0
{
  IndirectCommand cmd[];
} indirectSrc;

layout(binding = 1, std430) restrict buffer EngineInternal1
{
  uint       grow;
  uint       grow2;
  Descriptor desc[];
} mesh;

layout(binding = 2, std430) restrict buffer EngineInternal2
{
  uint    grow;
  uint    grow2;
  uint    heap[];
} var;

layout(binding = 3, std430) buffer EngineInternal3
{
  VkDrawIndexedIndirectCommand cmd[];
} indirect;

shared uint workId;
shared uint iboOffset;

void main() {
  const uint total = atomicAdd(mesh.grow, 0);

  while(true) {
    [[branch]]
    if(gl_LocalInvocationIndex==0) {
      uint at = atomicAdd(mesh.grow2, 1);
      if(at==total)
        atomicExchange(mesh.grow, 0);
      workId = at;
      }
    barrier();

    const uint at = workId;
    [[branch]]
    if(at>=total)
      return;

    const Descriptor d = mesh.desc[at];
    if(gl_LocalInvocationIndex==0) {
      iboOffset = atomicAdd(indirect.cmd[d.drawId].indexCount, d.indSz) + indirect.cmd[d.drawId].firstIndex;
      }
    barrier();

    [[loop]]
    for(uint i=gl_LocalInvocationIndex; i<d.indSz; i+=gl_WorkGroupSize.x) {
      var.heap[iboOffset+i] = var.heap[d.ptr+i];
      }
    }
  }
