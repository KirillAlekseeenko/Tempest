#version 450

#extension GL_EXT_control_flow_attributes:enable

layout(local_size_x = 64) in;

struct Descriptor
{
  uint drawId;
  uint ptr;
  uint indSz;
};

struct DrawIndexedIndirectCommand
{
  uint drawId;
  uint indexCountSrc;
  uint indexCount;
  uint instanceCount;
  uint firstIndex;    // prefix sum
  int  vertexOffset;  // can be abused to offset into var_buffer
  uint firstInstance; // caps: should be zero
  uint lutPtr;
};

layout(binding = 0, std430) restrict buffer EngineInternal0
{
  DrawIndexedIndirectCommand indirect[];
};

layout(binding = 1, std430) restrict buffer EngineInternal1
{
  uint       taskletCnt;
  uint       meshletCnt;
  uint       iterator;
  Descriptor desc[];
} mesh;

layout(binding = 2, std430) restrict buffer EngineInternal2
{
  uint       grow;
  uint       heap[];
} var;

layout(push_constant, std430) uniform UboPush {
  uint       indirectRate;
  uint       indirectCmdCount;
  };

shared uint workId;
shared uint iboOffset;

void main() {
  const uint first = 0; //mesh.taskletCnt;
  const uint total = mesh.meshletCnt;

  while(true) {
    [[branch]]
    if(gl_LocalInvocationIndex==0)
      workId = atomicAdd(mesh.iterator, 1);
    barrier();

    const uint at = workId;
    [[branch]]
    if(at>=total)
      break;

    const Descriptor d = mesh.desc[at+first];
    if(gl_LocalInvocationIndex==0) {
      uint idx = d.drawId*indirectRate;
      iboOffset = atomicAdd(indirect[idx].indexCount, d.indSz) + indirect[idx].firstIndex;
      }
    barrier();

    [[loop]]
    for(uint i=gl_LocalInvocationIndex; i<d.indSz; i+=gl_WorkGroupSize.x) {
      var.heap[iboOffset+i] = var.heap[d.ptr+i];
      }
    }

  if(gl_LocalInvocationIndex==0) {
    const uint maxGroups = (gl_NumWorkGroups.x * gl_NumWorkGroups.y * gl_NumWorkGroups.z);
    if(workId+1 == total+maxGroups) {
      // cleanup
      mesh.taskletCnt = 0;
      mesh.meshletCnt = 0;
      mesh.iterator   = 0;
      var.grow        = 0;
      }
    }
  }
